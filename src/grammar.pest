main = { SOI ~ (funcDecl | externFunc)* ~ EOI }

stmts = { "{" ~ expr* ~ "}" }

paramsDecl = { "(" ~ ")" |
             "(" ~ paramDecl ~ ("," ~ paramDecl)* ~ ")"
} 
paramDecl  = { varType ~ id }

funcDecl = { "fun" ~ id ~ paramsDecl ~ retType? ~ stmts }
    retType = { ":" ~ varType }

externFunc = { "extern fun" ~ id ~ typesList ~ retType? ~ ";" }
    typesList = { "(" ~ varType* ~ ")" } 

expr = { 
      varDeclExpr ~ ";"
    | unaryExpr ~ ";"
    | funCallExpr ~ ";"
    | ifExpr
    | returnExpr ~ ";"
    | loopExpr
    | whileExpr
    | breakExpr ~ ";"
    | assignExpr ~ ";"
    | binaryExpr ~ ";"
    // DEBUG | value
}
                
valuedExpr = _{ binaryExpr
                | unaryExpr
                | indexationExpr
                | funCallExpr
                | value 
}

value = { float | number | boolean | id | array }

varDeclExpr = { varType ~ id ~ "=" ~ valuedExpr }

assignExpr  = { (indexationExpr | id) ~ "=" ~ valuedExpr }

binaryExpr = { binaryTerm ~ (binaryOp ~ binaryTerm)+ }
binaryTerm = _{ (unaryExpr | indexationExpr | funCallExpr | value) | "(" ~ valuedExpr ~ ")" }
binaryOp = _{ add | subtract | multiply | divide | and 
              | or | leq | geq | eq | ne | lt | gt }

unaryExpr = { unaryOp ~ value
            | unaryOp ~ "(" ~ valuedExpr ~ ")"}
unaryOp = _{ not }

funCallExpr  = { id ~ params }
    params = { "(" ~ param* ~ ")" } 
    param =  { valuedExpr ~ "," ~ param | valuedExpr } 

ifExpr = { 
    ifBlock ~ elifBlock+ ~ elseBlock
    | ifBlock ~ elseBlock
    | ifBlock 
}
    ifBlock   = { "if" ~ valuedExpr ~  stmts }
    elifBlock = { "elif" ~ valuedExpr ~ stmts }
    elseBlock = { "else" ~ stmts }

returnExpr = { "ret" ~ valuedExpr }

loopExpr = { "loop" ~ stmts }

whileExpr = { "while" ~ valuedExpr ~ stmts }

breakExpr = { "break" }

indexationExpr = { id ~ ("[" ~ valuedExpr ~ "]")+ }


// primitive types 
number   = @{ ("-")? ~ ASCII_DIGIT+ }
float    = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
id       = @{ "_" * ~ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_" )* }
array    = { "[" ~ "]" |
             "[" ~ value ~ ("," ~ value)* ~ "]"
} 

boolean  = { "true" | "false" }
varType = { simpleType | arrayType }
simpleType  = { "i8" | "u8" | "i16" | "u16" 
                | "i32" | "u32" | "i64" | "u64" 
                | "bool" | "f32" | "f64" }
arrayType = { "[" ~ varType ~ ";" ~ number ~ "]" }

// operands
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide   = { "/" }
and      = { "&&" }
or       = { "||" }
not      = { "!" }
eq       = { "==" }
ne       = { "!=" }
lt       = { "<" }
gt       = { ">" }
leq      = { "<=" }
geq      = { ">=" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }
