main = { SOI ~ (funcDecl | externFunc | structDef)* ~ EOI }


// ------------ declarations  ------------ //

funcDecl = { visibility? ~ "fun" ~ id ~ paramsDecl ~ retType? ~ stmts }
    retType = { ":" ~ varType }
    paramsDecl = { "(" ~ ")" |
                "(" ~ paramDecl ~ ("," ~ paramDecl)* ~ ")"
    } 
paramDecl  = { varType ~ id }


externFunc = { "extern fun" ~ id ~ typesList ~ retType? ~ ";" }
    typesList = { "(" ~ varType* ~ ")" } 

structDef = { visibility? ~ "struct" ~ id ~ structMembersDef }
    structMembersDef = _{ 
        "{" ~ "}"
        | "{" ~ paramDecl ~ ("," ~ paramDecl)* ~ "}" 
        | "{" ~ (paramDecl ~ ",")+ ~ "}" 
    }


// ------------ statements  ------------ //

stmts = { "{" ~ stmt* ~ "}" }

stmt = { 
    varDeclStmt ~ ";"
    | funCallExpr ~ ";"
    | ifStmt
    | returnStmt ~ ";"
    | loopStmt
    | whileStmt
    | breakStmt ~ ";"
    | assignStmt ~ ";"
    | funCallExpr ~ ";"
}
                
varDeclStmt = { varType ~ id ~ "=" ~ expr }

assignStmt  = { (membAccessExpr | id) ~ "=" ~ expr }

ifStmt = { ifBlock ~ (elifBlock* ~ elseBlock)? }
    ifBlock   = { "if" ~ expr ~  stmts }
    elifBlock = { "elif" ~ expr ~ stmts }
    elseBlock = { "else" ~ stmts }

returnStmt = { "ret" ~ expr }

loopStmt = { "loop" ~ stmts }

whileStmt = { "while" ~ expr ~ stmts }

breakStmt = { "break" }


// ------------ expressions  ------------ //

expr = _{ 
    binaryExpr
    | unaryExpr
    | membAccessExpr
    | funCallExpr
    | value 
}

binaryExpr = { binaryTerm ~ (binaryOp ~ binaryTerm)+ }
binaryTerm = _{ (unaryExpr
                    | membAccessExpr 
                    | funCallExpr 
                    | value) 
                | "(" ~ expr ~ ")" }
binaryOp = _{ add | subtract | multiply | divide | and 
              | or | leq | geq | eq | ne | lt | gt }

unaryExpr = { unaryOp ~ ( expr | "(" ~ expr ~ ")" ) }
unaryOp = _{ not | reference | deref }

funCallExpr  = { id ~ params }
    params = { "(" ~ param* ~ ")" } 
    param =  { expr ~ "," ~ param | expr } 

membAccessExpr = { (funCallExpr | id) ~ (member | indice)+ }
member = { "." ~ id }
indice = { "[" ~ expr ~ "]" }


// ------------ literal values  ------------ //

value = { float | number | boolean | strct | id | array }

number   = @{ ("-")? ~ ASCII_DIGIT+ }
float    = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
id       = @{ "_" * ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }
array    = { "[" ~ "]" |
             "[" ~ expr ~ ("," ~ expr)* ~ "]"
} 
strct = { id ~ structMembers }
    memberDecl = { id ~ "=" ~ expr } 
    structMembers = _{ 
        "{" ~ "}"
        | "{" ~ memberDecl ~ ("," ~ memberDecl)* ~ "}" 
        | "{" ~ (memberDecl ~ ",")+ ~ "}" 
    }

boolean  = { "true" | "false" }


// ------------ misc ------------ //

varType = { refType | simpleType | arrayType }
    refType = { "&" ~ varType }
    simpleType = { id }
    arrayType = { "[" ~ varType ~ ";" ~ number ~ "]" }

visibility = { "pub" }

// operands
add       = { "+" }
subtract  = { "-" }
multiply  = { "*" }
divide    = { "/" }
and       = { "&&" }
or        = { "||" }
not       = { "!" }
eq        = { "==" }
ne        = { "!=" }
lt        = { "<" }
gt        = { ">" }
leq       = { "<=" }
geq       = { ">=" }
reference = { "&" }
deref     = { "*" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }
