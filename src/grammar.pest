main = { SOI ~ (funcDecl | externFunc | structDef | enumDef | useModules)* ~ EOI }


// ------------ declarations  ------------ //

funcDecl = { visibility? ~ "fun" ~ id ~ paramsDecl ~ retType? ~ stmts }
    retType = { ":" ~ varType }
    paramsDecl = { "(" ~ ")" |
                "(" ~ paramDecl ~ ("," ~ paramDecl)* ~ ")"
    } 
paramDecl  = { varType ~ id }

externFunc = { visibility? ~ "extern fun" ~ id ~ typesList ~ retType? ~ ";" }
    typesList = { "(" ~ varType* ~ ")" } 

structDef = { visibility? ~ "struct" ~ id ~ structMembersDef }
    structMembersDef = _{ 
        "{" ~ "}"
        | "{" ~ paramDecl ~ ("," ~ paramDecl)* ~ "}" 
        | "{" ~ (paramDecl ~ ",")+ ~ "}" 
    }

enumDef = {visibility? ~ "enum" ~ id ~ "{" ~ (enumVariant ~ ",")* ~ enumVariant? ~ "}" }
    enumVariant = { id ~ namedFileds? }
    namedFileds = { "{" ~ (paramDecl ~ ",")* ~ paramDecl? ~ "}" }

useModules = { "use" ~ module ~ ("," ~ module)* ~ ","?}
module = { id ~ ("/" ~ id)*}

// ------------ statements  ------------ //

stmts = { "{" ~ stmt* ~ "}" }

stmt = { 
    varDeclStmt ~ ";"
    | funCallExpr ~ ";"
    | ifLetStmt
    | ifStmt
    | returnStmt ~ ";"
    | loopStmt
    | whileStmt
    | breakStmt ~ ";"
    | assignStmt ~ ";"
}
                
varDeclStmt = { "let" ~ id ~ (":" ~ varType)? ~ "=" ~ expr }

assignStmt  = { (membAccessExpr | unaryExpr | id) ~ "=" ~ expr }

ifStmt = { ifBlock ~ (elifBlock* ~ elseBlock)? }
    ifBlock   = { "if" ~ expr ~  stmts }
    elifBlock = { "elif" ~ expr ~ stmts }
    elseBlock = { "else" ~ stmts }

returnStmt = { "ret" ~ expr }

loopStmt = { "loop" ~ stmts }

whileStmt = { "while" ~ expr ~ stmts }

breakStmt = { "break" }

ifLetStmt = { "if let" ~ enm ~ "=" ~ expr ~ stmts ~ ("else" ~ stmts)?}


// ------------ expressions  ------------ //

expr = _{ 
    binaryExpr
    | membAccessExpr
    | unaryExpr
    | funCallExpr
    | value 
}

binaryExpr = { binaryTerm ~ (binaryOp ~ binaryTerm)+ }
binaryTerm = _{ (unaryExpr
                    | membAccessExpr 
                    | funCallExpr 
                    | value) 
                | "(" ~ expr ~ ")" }
binaryOp = _{ add | subtract | multiply | divide | and 
              | or | leq | geq | eq | ne | lt | gt }

unaryExpr = { unaryOp ~ ( exprExceptBinary | "(" ~ expr ~ ")" ) }
// unary exprs. over binary and memb. access exprs. must contain the expr. between parenthesis
exprExceptBinary = _{
    // membAccessExpr
    unaryExpr
    | funCallExpr
    | value 
} 
unaryOp = _{ not | reference | deref }

funCallExpr  = { (builtinFuncId | normalFuncId) ~ params }
    normalFuncId = { id }
    builtinFuncId = @{ "@" ~ id }
    params = { "(" ~ param* ~ ")" } 
    param =  { valueOrType ~ "," ~ param | valueOrType } 
    valueOrType = { binaryExpr | membAccessExpr | funCallExpr 
                    | varType | unaryExpr | value }

membAccessExpr = { (unaryExpr | funCallExpr | id) ~ (member | indice | range)+ }
member = { "." ~ id }
indice = { "[" ~ expr ~ "]" }
range = { "[" ~ rangeVal ~ ".." ~ rangeVal ~ "]" }
rangeVal = { expr? }

// ------------ literal values  ------------ //

value = { float | number | array | enm | strct | boolean | id }

number   = @{ ("-")? ~ ASCII_DIGIT+ }
float    = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
id       = @{ !reserved ~ "_" * ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }
array    = { "[" ~ "]" |
             "[" ~ expr ~ ("," ~ expr)* ~ "]"
} 

boolean  = { "true" | "false" }

strct = { id ~ structMembers }
    memberDecl = { id ~ "=" ~ expr } 
    structMembers = _{ 
        "(" ~ ")"
        | "{" ~ memberDecl ~ ("," ~ memberDecl)* ~ "}" 
        | "{" ~ (memberDecl ~ ",")+ ~ "}" 
    }

enm = { id ~ ":" ~ id ~ structMembers? } // enum


// ------------ misc ------------ //

varType = { refType | simpleType | arrayType | sliceType }
    refType = { "&" ~ varType }
    arrayType = { "[" ~ varType ~ ";" ~ number ~ "]" }
    sliceType = { "[" ~ varType ~ "]" }
    simpleType = @{ "_" * ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }

visibility = { "pub" }

// operands
add       = { "+" }
subtract  = { "-" }
multiply  = { "*" }
divide    = { "/" }
and       = { "&&" }
or        = { "||" }
not       = { "!" }
eq        = { "==" }
ne        = { "!=" }
lt        = { "<" }
gt        = { ">" }
leq       = { "<=" }
geq       = { ">=" }
reference = { "&" }
deref     = { "*" }

reserved = { intTypes | ("f32" | "f64") | "c_voidptr" | keywords}
    intTypes = @{ ("i" | "u") ~ ("8" | "16" | "32" | "64") }
    keywords = { "if" | "while" | "loop" | "for" | "fun" 
                    | "struct" | "enum" | "use" | "break" | "ret" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }
