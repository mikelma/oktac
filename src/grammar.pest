main = { SOI ~ stmts ~ EOI }

stmts = { expr* }

expr = { 
      assignExpr ~ ";"
    | mathExpr ~ ";"
    | printExpr ~ ";"
    | ifElseExpr
    | forExpr
    | returnExpr ~ ";"
    | value
}

value = { number | id }

mathExpr = { mathTerm ~ (mathOp ~ mathTerm)+ }
mathTerm = _{ value | "(" ~ mathExpr ~ ")" }
mathOp = _{ add | subtract | multiply | divide }

assignExpr  = { id ~ "=" ~ (mathExpr | value) }

printExpr  = { "print(" ~ (mathExpr | value) ~ ")" }

ifElseExpr = { "if" ~ (mathExpr | value) ~ "{" ~ stmts ~ "}" ~ "else" ~ "{" ~ stmts ~ "}" }

forExpr = { "for" ~ id ~ "in" ~ iterator ~ "{" ~ stmts ~ "}" }

returnExpr = { "ret" ~ (mathExpr | value) }

iterator = { intRange }
    intRange = { value ~ ".." ~ value }

number   = @{ ("-")? ~ ASCII_DIGIT+ }
id       = @{ ASCII_ALPHA + }
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide   = { "/" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }
