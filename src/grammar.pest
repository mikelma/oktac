main = { SOI ~ funcDecl* ~ EOI }

stmts = { "{" ~ expr* ~ "}" }

paramsDecl = { "(" ~ (paramDecl ~ "," ~ paramDecl | paramDecl)* ~ ")" }
    paramDecl  = { varType ~ id }

funcDecl = { "fun" ~ id ~ paramsDecl ~ ":" ~ varType  ~ stmts }

expr = { 
      varDeclExpr ~ ";"
    | assignExpr ~ ";"
    | binaryExpr ~ ";"
    | unaryExpr ~ ";"
    | funCallExpr ~ ";"
    | ifElseExpr
    | forExpr
    | returnExpr ~ ";"
    | value
}
valuedExpr = _{ binaryExpr | unaryExpr | funCallExpr | value }
value = { number | boolean | id }

varDeclExpr = { varType ~ id ~ "=" ~ valuedExpr }

assignExpr  = { id ~ "=" ~ valuedExpr }

binaryExpr = { binaryTerm ~ (binaryOp ~ binaryTerm)+ }
binaryTerm = _{ (value | unaryExpr) | "(" ~ valuedExpr ~ ")" }
binaryOp = _{ add | subtract | multiply | divide | and 
              | or | leq | geq | eq | lt | gt }

unaryExpr = { unaryOp ~ value
            | unaryOp ~ "(" ~ valuedExpr ~ ")"}
unaryOp = _{ not }

funCallExpr  = { id ~ params }
    params = { "(" ~ param* ~ ")" } 
    param =  { valuedExpr ~ "," ~ param | valuedExpr } 

ifElseExpr = { "if" ~ valuedExpr ~  stmts ~ "else" ~ stmts }

forExpr = { "for" ~ id ~ "in" ~ iterator ~ stmts }

returnExpr = { "ret" ~ valuedExpr }

iterator = { intRange }
    intRange = { value ~ ".." ~ value }


// primitive types 
number   = @{ ("-")? ~ ASCII_DIGIT+ }
id       = @{ ASCII_ALPHA + }
boolean  = { "true" | "false" }
varType     = { "i32" | "bool" }

// operands
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide   = { "/" }
and      = { "&&" }
or       = { "||" }
not      = { "!" }
eq       = { "==" }
lt       = { "<" }
gt       = { ">" }
leq      = { "<=" }
geq      = { ">=" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE }
